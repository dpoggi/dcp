#
# oh-my-zsh plugins
#

# Standard plugins
plugins=(history jsontools zsh-syntax-highlighting)

# Conditional plugins
if [[ " ${TERM} " = *"256color"* ]]; then
  plugins+=(emoji)
fi
if [[ "${DCP_OS}" = "Darwin" ]]; then
  plugins+=(macos)
fi

# User-specified plugins
if [[ -s "${DCP_CONFIG_DIR}/plugins.lst" ]]; then
  while IFS='' read -r; do
    plugins+=("${REPLY}")
  done <"${DCP_CONFIG_DIR}/plugins.lst"
fi

# Conditionally initialize Homebrew
if [[ "${DCP_OS}" = "Darwin" ]]; then
  . "${DCP}/lib/brew.sh"
fi

# Load most of dcp
. "${DCP}/lib/init.sh"

# Remove broken Homebrew Git completions before they cause a problem
if [[ "${DCP_OS}" = "Darwin" ]]; then
  if [[ -h "/usr/local/share/zsh/site-functions/_git" ]]; then
    rm -f "/usr/local/share/zsh/site-functions/_git"
  elif [[ -h "/opt/homebrew/share/zsh/site-functions/_git" ]]; then
    rm -f "/opt/homebrew/share/zsh/site-functions/_git"
  fi
fi

# Load oh-my-zsh
. "${ZSH}/oh-my-zsh.sh"

# Zap uninstall function (why make it this easy to run?)
unset -f uninstall_oh_my_zsh 2>/dev/null || :

# GitHub CLI
if __is_command hub; then
  git() { hub "$@"; }
fi

# Completion aliasing
if __is_function compdef; then
  compdef _git ga=git-add
  compdef _git gb=git-branch
  compdef _git gc=git-commit
  compdef _git gcane=git-commit
  compdef _git gcm=git-commit
  compdef _git gco=git-checkout
  compdef _git gd=git-diff
  compdef _git gds=git-diff
  compdef _git gdt=git-describe
  compdef _git gf=git-fetch
  compdef _git ggr=git-grep
  compdef _git gl=git-log
  compdef _git gll=git-log
  compdef _git gm=git-merge
  compdef _git gp=git-push
  compdef _git gpl=git-pull
  compdef _git gr=git-rebase
  compdef _git grv=git-remote
  compdef _git gs=git-status
  compdef _git gsa=git-stash
  compdef _git gsl=git-stash
  compdef _git gst=git-stash

  if __is_command hub; then
    compdef hub=git
  fi
fi

# fzf, if applicable
if [[ -s "${HOME}/.fzf.zsh" ]]; then
  . "${HOME}/.fzf.zsh"
fi

# Enable fallback behavior when glob matching fails
unsetopt nomatch

# Finish up the details
. "${DCP}/lib/post_init.sh"
